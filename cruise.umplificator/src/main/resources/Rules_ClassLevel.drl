package cruise.umplificator.rules

import org.eclipse.jdt.core.dom.*;
import cruise.umple.compiler.*;
import cruise.umplificator.parser.*;
import java.util.*;


rule "addClass_toUmpleModel"
  salience 100
	when
		umpleClass: UmpleClass()
		umpleModel: UmpleModel()
	then
		//System.out.println("ADDING CLASS TO MODEL: "+umpleClass.getName());
	    umpleModel.addUmpleClass(umpleClass);
end

rule "transform_ImportDeclaration"
  salience 100
	when
		importDeclaration: ImportDeclaration()
		uClass: UmpleClass()
		eval(uClass.getName().equals(getDeclarationContainerName(importDeclaration)))		
	then
		//System.out.println("ADDING DEPEND TO CLASS: "+uClass.getName());
		Depend depend = new Depend(getImportName(importDeclaration));
		uClass.addDepend(depend);
end

rule "transform_ImportDeclaration_UInterface"
  salience 100
	when
		importDeclaration: ImportDeclaration()
		uInterface: UmpleInterface()
		eval(uInterface.getName().equals(getDeclarationContainerName(importDeclaration)))	
	then
		Depend depend = new Depend(getImportName(importDeclaration));
		uInterface.addDepend(depend);
end

rule "transform_InheritanceDeclaration_Interfaces"
  salience 100
	when
		typeDeclaration : TypeDeclaration(typeDeclaration.superInterfaceTypes().size() > 0)
		uClass: UmpleClass(typeDeclaration.getName().getIdentifier().equals(uClass.getName()))
	then
		List<String> parentInterfaces = getImplementedInterfaces(typeDeclaration);
	 	for (String interfaceName : parentInterfaces)
		{
			UmpleInterface aInterface = new UmpleInterface(interfaceName);
		    System.out.println(interfaceName);
			uClass.addParentInterface(aInterface);
		}
end

rule "transform_InheritanceDeclaration_Interfaces_UInterface"
  salience 100
	when
		typeDeclaration : TypeDeclaration(typeDeclaration.superInterfaceTypes().size() > 0)
		uInterface: UmpleInterface(typeDeclaration.getName().getIdentifier().equals(uInterface.getName()))
	then
		List<String> parentInterfaces = getImplementedInterfaces(typeDeclaration);
	 	for (String interfaceName : parentInterfaces)
		{
			UmpleInterface aInterface = new UmpleInterface(interfaceName);
		    System.out.println(interfaceName);
			uInterface.addExtendsInterface(aInterface);
		}
end

rule "transform_InheritanceDeclaration_SuperClasses"
  salience 100
	when
		typeDeclaration : TypeDeclaration(typeDeclaration.getSuperclassType() != null)
		uClass: UmpleClass(typeDeclaration.getName().getIdentifier().equals(uClass.getName()))
	then
		UmpleClass superClass = new UmpleClass(getSuperClassName(typeDeclaration));
		uClass.setExtendsClass(superClass);
end

rule "transform_Namespace"
  salience 100
	when
		packageDeclaration: PackageDeclaration()
		uClass: UmpleClass()
		eval(uClass.getName().equals(getDeclarationContainerName(packageDeclaration)))	
	then
		uClass.addNamespace(packageDeclaration.getName().getFullyQualifiedName());
end


rule "transform_Namespace_UInterface"
  salience 100
	when
		packageDeclaration: PackageDeclaration()
		uInterface: UmpleInterface()
		eval(uInterface.getName().equals(getDeclarationContainerName(packageDeclaration)))	
	then
		uInterface.addNamespace(packageDeclaration.getName().getFullyQualifiedName());
end

rule "append_FieldDeclaration"
  salience 100
	when
		fieldDeclaration: FieldDeclaration()
		uClass: UmpleClass()
		eval(uClass.getName().equals(getDeclarationContainerName(fieldDeclaration)))
	then
		uClass.appendExtraCode(fieldDeclaration.toString());
end

rule "append_FieldDeclaration_interfaces"
  salience 100
	when
		fieldDeclaration: FieldDeclaration()
		uInterface: UmpleInterface()
		eval(uInterface.getName().equals(getDeclarationContainerName(fieldDeclaration)))
	then
		uInterface.appendExtraCode(fieldDeclaration.toString());
end

rule "append_MethodDeclaration"
  salience 100
	when
		method: MethodDeclaration()
		uClass: UmpleClass()
		eval(uClass.getName().equals(getDeclarationContainerName(method)))
	then
		uClass.appendExtraCode(method.toString());
end

rule "append_MethodDeclaration_interfaces"
  salience 100
	when
		method: MethodDeclaration()
		uInterface: UmpleInterface()
		eval(uInterface.getName().equals(getDeclarationContainerName(method)))
	then
		uInterface.appendExtraCode(method.toString());
end


//http://svn.apache.org/repos/asf/geronimo/devtools/j2g/trunk/plugins/org.apache.geronimo.devtools.j2g.sources/src/org/apache/geronimo/devtools/j2g/sources/dependence/NodesVisitor.java
function String getImportName(ImportDeclaration importDeclaration){
	StringBuilder importName =  new StringBuilder();
	importName.append(importDeclaration.getName().getFullyQualifiedName());
	if (importDeclaration.isOnDemand()) { 
		importName.append(".*"); 
	}else { 
		importName.append("");
	}
	return importName.toString();
}

function String getSuperClassName(TypeDeclaration typeDecl)
{
    Type type = typeDecl.getSuperclassType();
    String superClassname="";
	if (type != null)
	{
		if (type.isSimpleType())
		{
		SimpleType simpleType = (SimpleType) type;
		superClassname = simpleType.getName().getFullyQualifiedName();
		}
	}
	return superClassname;
}

function List<String> getImplementedInterfaces(TypeDeclaration typeDeclaration)
{
	List<String> names = new ArrayList<String>();
	List<Type> interfaceTypes = typeDeclaration.superInterfaceTypes();
	for (Type aType : interfaceTypes)
	{
		if (aType !=null && aType.isSimpleType())
		{
			SimpleType simpleType = (SimpleType) aType;
		    String interfaceName = simpleType.getName().getFullyQualifiedName();
		    names.add(interfaceName);
		}
	}
	return names;
}

function String getDeclarationContainerName(ASTNode declaration)
{
	if  ((declaration instanceof MethodDeclaration)   || 
		 (declaration instanceof FieldDeclaration)) {
		ASTNode parentNode = declaration.getParent();
		if (parentNode.getNodeType() == ASTNode.TYPE_DECLARATION)
		{
			TypeDeclaration container = (TypeDeclaration) parentNode;
			String javaClassName = container.getName().getFullyQualifiedName();
			System.out.println(javaClassName);
			return javaClassName;
		}
	}
	else if ((declaration instanceof ImportDeclaration)|| (declaration instanceof PackageDeclaration))
	{
		ASTNode parentNode = declaration.getParent();
	    if (parentNode.getNodeType() == ASTNode.COMPILATION_UNIT)
		{
			CompilationUnit unit = (CompilationUnit) parentNode;
			JavaParser javaParser =new JavaParser();
			javaParser.parseUnit(parentNode.toString());
			String compilationUnitName= javaParser.getJavaVisitor().getTypeDeclaration().getName().getFullyQualifiedName();
			return compilationUnitName;
		}			 
	}
	return "";
}


